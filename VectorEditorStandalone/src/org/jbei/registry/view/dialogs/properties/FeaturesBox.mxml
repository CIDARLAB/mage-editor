<?xml version="1.0" encoding="utf-8"?>
<mx:Box 
	label="Features"
	width="400" 
	height="300" 
	initialize="onInitialize(event)"
	xmlns:mx="http://www.adobe.com/2006/mxml">

	<mx:Script>
		<![CDATA[
            import mx.collections.ArrayCollection;
            import mx.controls.dataGridClasses.DataGridItemRenderer;
            import mx.utils.StringUtil;
            
            import org.jbei.bio.sequence.dna.Feature;
            import org.jbei.lib.ui.dialogs.ModalDialog;
            import org.jbei.lib.ui.dialogs.ModalDialogEvent;
            import org.jbei.registry.ApplicationFacade;
            import org.jbei.registry.Notifications;
            import org.jbei.registry.view.dialogs.FeatureDialogForm;
			
			[Bindable]
			private var filteredFeatures:ArrayCollection = new ArrayCollection();
			
			[Bindable]
			private var query:String;
			
			private var features:ArrayCollection;
			
			private function onInitialize(event:Event):void
			{
				if(! ApplicationFacade.getInstance().sequenceProvider) return;
				
				features = ApplicationFacade.getInstance().sequenceProvider.features;
				
				searchTextInput.setFocus();
				
				updateFeaturesList();
			}
			
			private function onSearchTextInputChange(event:Event):void
			{
				query = searchTextInput.text;
				
				updateFeaturesList();
			}
			
			private function onFeaturesDataGridChange(event:Event):void
			{
				updateButtons();
			}
			
			private function onNewFeatureButtonClick(event:MouseEvent):void
			{
				var featureDialog:ModalDialog = new ModalDialog(FeatureDialogForm, null);
				featureDialog.title = "Create New Feature";
				featureDialog.open();
				
				featureDialog.addEventListener(ModalDialogEvent.SUBMIT, onFeatureDialogSubmit);
			}
			
			private function onEditFeatureButtonClick(event:MouseEvent):void
			{
				if(filteredFeatures.length == 0 && featuresDataGrid.selectedItems.length == 0) return;
				
				var featureDialog:ModalDialog = new ModalDialog(FeatureDialogForm, featuresDataGrid.selectedItem.feature as Feature);
				featureDialog.title = "Edit Feature";
				featureDialog.open();
				
				featureDialog.addEventListener(ModalDialogEvent.SUBMIT, onFeatureDialogSubmit);
			}
			
			private function onRemoveFeatureButtonClick(event:MouseEvent):void
			{
				if(featuresDataGrid.selectedItems.length == 0) return;
				
				var featuresToRemove:Array = new Array();
				for(var i:int = 0; i < featuresDataGrid.selectedItems.length; i++) {
					featuresToRemove.push(featuresDataGrid.selectedItems[i].feature as Feature);
				}
				
				ApplicationFacade.getInstance().sequenceProvider.removeFeatures(featuresToRemove);
				
				updateFeaturesList();
			}
			
			private function onSearchTextInputKeyDown(event:KeyboardEvent):void
			{
				if(event.keyCode == Keyboard.DOWN) {
					featuresDataGrid.setFocus();
					
					if(filteredFeatures.length > 0 && featuresDataGrid.selectedIndex == -1) {
						featuresDataGrid.selectedIndex = 0;
					}
					
					updateButtons();
				}
			}
			
			private function updateFeaturesList():void
			{
				filteredFeatures.removeAll();
				
				for(var i:int = 0; i < features.length; i++) {
					var feature:Feature = features[i] as Feature;
					
					var searchQuery:String = StringUtil.trim(query).toLowerCase();
					
					var strand:String = "";
					if(feature.strand == 1) {
						strand = "+";
					} else if(feature.strand == -1) {
						strand = "-";
					}
					
					if(searchQuery == "") {
						filteredFeatures.addItem({name : feature.name, "feature" : feature, position : ((feature.start + 1) + " - " + (feature.end)), type : feature.type, strand : strand});
					} else {
						if(feature.name.toLowerCase().search(searchQuery) >= 0 || feature.type.toLowerCase().search(searchQuery) >= 0) {
							filteredFeatures.addItem({name : feature.name, "feature" : feature, position : ((feature.start + 1) + " - " + (feature.end)), type : feature.type, strand : strand});
						}
					}
				}
				
				updateButtons();
			}
			
			private function updateButtons():void
			{
				editFeatureButton.enabled = featuresDataGrid.selectedIndex >= 0 && featuresDataGrid.selectedItems.length == 1;
				removeFeatureButton.enabled = featuresDataGrid.selectedIndex >= 0;
			}
			
			private function onFocusIn(event:FocusEvent):void
			{
				if(searchTextInput.text == "Enter Name or Type") {
					searchTextInput.text = "";
					searchTextInput.setStyle("color", "#000000");
				}
			}
			
			private function onFocusOut(event:FocusEvent):void
			{
				if(searchTextInput.text == "") {
					searchTextInput.text = "Enter Name or Type";
					searchTextInput.setStyle("color", "#808080");
				}
			}
			
			private function onFeatureDialogSubmit(event:ModalDialogEvent):void
			{
				features = ApplicationFacade.getInstance().sequenceProvider.features;
				
				updateFeaturesList();
			}
			
			private function onFeaturesDataGridDoubleClick(event:MouseEvent):void
			{
				if(event.target is DataGridItemRenderer) {
					var feature:Feature = (event.target as DataGridItemRenderer).data.feature;
					
					ApplicationFacade.getInstance().sendNotification(Notifications.SELECTION_CHANGED, new Array(feature.start, feature.end));
				}
			}
			
			public function doCaselessSortForField(field:String):Function
			{
				return function(obj1:Object, obj2:Object):int
				{
					return mx.utils.ObjectUtil.stringCompare(obj1[field],obj2[field],true);
				}
			}
		]]>
	</mx:Script>
	
	<mx:VBox width="100%" height="100%" paddingBottom="1" paddingTop="1" paddingLeft="1" paddingRight="1">
		<mx:TextInput id="searchTextInput" width="200" change="onSearchTextInputChange(event)" keyDown="onSearchTextInputKeyDown(event)" focusIn="onFocusIn(event)" focusOut="onFocusOut(event)" />
		
		<mx:HBox width="100%" height="100%">
			<mx:DataGrid 
				id="featuresDataGrid" 
				dataProvider="{ filteredFeatures }" 
				change="onFeaturesDataGridChange(event)" 
				allowMultipleSelection="true" 
				doubleClickEnabled="true" 
				height="100%" 
				width="100%" 
				doubleClick="onFeaturesDataGridDoubleClick(event)">
				
				<mx:columns>
					<mx:DataGridColumn dataField="name" headerText="Name" width="150" sortCompareFunction="doCaselessSortForField('name')"/>
					<mx:DataGridColumn dataField="type" headerText="Type" sortCompareFunction="doCaselessSortForField('type')"/>
					<mx:DataGridColumn dataField="position" headerText="Position" />
					<mx:DataGridColumn dataField="strand" headerText="Strand" width="50" />
				</mx:columns>
			</mx:DataGrid>
			
			<mx:VBox height="100%">
				<mx:Button id="newFeatureButton" label="New" width="80" click="onNewFeatureButtonClick(event)" />
				<mx:Button id="editFeatureButton" label="Edit" width="80" click="onEditFeatureButtonClick(event)" />
				<mx:Button id="removeFeatureButton" label="Remove" width="80" click="onRemoveFeatureButtonClick(event)" />
			</mx:VBox>
		</mx:HBox>
	</mx:VBox>
</mx:Box>
